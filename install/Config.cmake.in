@PACKAGE_INIT@


function(@PROJECT_NAME@_include_config_files target)

    foreach(config_name ${ARGN})

        set(config_filename "${target}${config_name}.cmake")
        set(config_filepath "${CMAKE_CURRENT_LIST_DIR}/${target}/${config_filename}")

        if(EXISTS "${config_filepath}")
            include("${config_filepath}")
        endif()

    endforeach()

    if(NOT DEFINED ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE)
        set(@PROJECT_NAME@_${target}_FOUND 1 PARENT_SCOPE)
    else()
        message(WARNING ${${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE})
    endif()

endfunction()






# define available component
set(@PROJECT_NAME@_supported_components @__CMUT__INSTALL__PROJECT_SUPPORTED_COMPONENTS@)

@__CMUT__INSTALL__PER_COMPONENT_DEPENDENCIES@

# search dependencies
include(CMakeFindDependencyMacro)
set(find_dependencies_script_file "${CMAKE_CURRENT_LIST_DIR}/dependencies/find_dependencies.cmake")
if(EXISTS "${find_dependencies_script_file}")
    include("${find_dependencies_script_file}")
else()
    # keep for backware compatibility
    set(find_dependencies_script_file "${CMAKE_CURRENT_LIST_DIR}/dependencies/include_all.cmake")
    if(EXISTS "${find_dependencies_script_file}")
        include("${find_dependencies_script_file}")
    endif()
endif()


foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})

    if (NOT "${@PROJECT_NAME@_supported_components}" MATCHES ${component})
        set(@PROJECT_NAME@_FOUND False)
        list(APPEND @PROJECT_NAME@_unsupported_components ${component})
    else()
        foreach(dependency IN LISTS ${component}_dependencies)
            @PROJECT_NAME@_include_config_files( ${dependency} Target)
        endforeach()
        @PROJECT_NAME@_include_config_files( ${component} Target)
    endif()

endforeach()

if(@PROJECT_NAME@_unsupported_components)
    set(@PROJECT_NAME@_NOTFOUND_MESSAGE "Unsupported component: ${@PROJECT_NAME@_unsupported_components}")
    message(ERROR "@PROJECT_NAME@: ${@PROJECT_NAME@_NOTFOUND_MESSAGE}")
endif()

check_required_components("@PROJECT_NAME@")
